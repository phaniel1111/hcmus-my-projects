/*----------------------------------------------------------
MASV: 20120313
HO TEN: Phan Tan Kiet
LAB: 03
NGAY: 06/04/2023
----------------------------------------------------------*/
USE master
GO
IF DB_ID('QLSV') IS NOT NULL
	DROP DATABASE QLSV
GO
CREATE DATABASE QLSV
GO
USE QLSV
GO
-- CREATE TABLES

CREATE TABLE SINHVIEN(
	MASV NVARCHAR(20) NOT NULL,
	HOTEN NVARCHAR(100) NOT NULL,
	NGAYSINH DATETIME,
	DIACHI NVARCHAR(200),
	MALOP VARCHAR(20),
	TENDN NVARCHAR(100) NOT NULL,
	MATKHAU VARBINARY(100) NOT NULL,
	PRIMARY KEY(MASV)
	)
GO
CREATE TABLE NHANVIEN(
	MANV VARCHAR(20) NOT NULL,
	HOTEN NVARCHAR(100) NOT NULL,
	EMAIL VARCHAR(20),
	LUONG VARBINARY(100),
	TENDN NVARCHAR(100) NOT NULL,
	MATKHAU VARBINARY(100) NOT NULL,
	PRIMARY KEY(MANV)
	)
GO
CREATE TABLE LOP(
	MALOP VARCHAR(20) NOT NULL,
	TENLOP NVARCHAR(100) NOT NULL,
	MANV VARCHAR(20),
	PRIMARY KEY(MALOP)
	)
GO

-- TAO PROCUREDURES
-- i
IF OBJECT_ID('dbo.SP_INS_SINHVIEN','P') IS NOT NULL
DROP PROCEDURE dbo.SP_INS_SINHVIEN
GO
CREATE PROCEDURE dbo.SP_INS_SINHVIEN
	@MASV NVARCHAR(20),
	@HOTEN NVARCHAR(100),
	@NGAYSINH DATETIME,
	@DIACHI NVARCHAR(200),
	@MALOP VARCHAR(20),
	@TENDN NVARCHAR(100),
	@MATKHAU NVARCHAR(50)
AS
BEGIN
	INSERT INTO SINHVIEN(MASV, HOTEN, NGAYSINH, DIACHI, MALOP, TENDN, MATKHAU)
	VALUES (@MASV, @HOTEN, @NGAYSINH, @DIACHI, @MALOP, @TENDN, HASHBYTES('MD5',@MATKHAU))
END
GO
--EXEC dbo.SP_INS_SINHVIEN 'SV01', 'NGUYEN VAN A', '1990-01-01', '280 AN DUONG VUONG', 'CNTT-K35', 'SVA', '123456' -- Fix date format
--SELECT * FROM SINHVIEN
--select * from sinhvien where tendn='SVA' and matkhau=HASHBYTES('MD5',N'123456')
GO

-- ii
--Buoc 1
IF NOT EXISTS
(
	SELECT * FROM sys.symmetric_keys WHERE symmetric_key_id = 101
)
CREATE MASTER KEY ENCRYPTION BY
PASSWORD= '20120313'
GO
-- Buoc 2
IF NOT EXISTS
(
	SELECT * FROM sys.certificates WHERE name = 'Cert'
)
CREATE CERTIFICATE Cert
WITH SUBJECT = 'Cert';
GO
-- Buoc 3
IF NOT EXISTS
(
	SELECT * FROM sys.symmetric_keys WHERE name = 'Prikey'
)
CREATE SYMMETRIC KEY Prikey
WITH ALGORITHM = AES_256 
ENCRYPTION BY CERTIFICATE Cert;
GO
-- Buoc 4
IF OBJECT_ID('SP_INS_NHANVIEN','P') IS NOT NULL
DROP PROCEDURE SP_INS_NHANVIEN
GO
CREATE PROCEDURE SP_INS_NHANVIEN
	@MANV VARCHAR(20),
	@HOTEN NVARCHAR(100),
	@EMAIL VARCHAR(20),
	@LUONG INT,
	@TENDN NVARCHAR(100),
	@MATKHAU NVARCHAR(100)
AS
BEGIN
    OPEN SYMMETRIC KEY Prikey
    DECRYPTION BY CERTIFICATE Cert;
    DECLARE @LUONG_VARBINARY VARBINARY(100) = CONVERT(VARBINARY(100), @LUONG)
    INSERT INTO NHANVIEN (MANV, HOTEN, EMAIL, LUONG, TENDN, MATKHAU)
    VALUES (@MANV, @HOTEN, @EMAIL, ENCRYPTBYKEY(KEY_GUID('Prikey'), @LUONG_VARBINARY), @TENDN, HASHBYTES('SHA1', @MATKHAU));
    CLOSE SYMMETRIC KEY Prikey
END
GO
--EXEC SP_INS_NHANVIEN 'NV01', 'NGUYEN VAN A', 'NVA@', 3000000,'NVA', 'abcd12'
--SELECT * FROM NHANVIEN
--select * from nhanvien where tendn='NVA' and matkhau=HASHBYTES('SHA1',N'abcd12')

-- iii
IF OBJECT_ID('SP_SEL_NHANVIEN','P') IS NOT NULL
DROP PROCEDURE SP_SEL_NHANVIEN
GO
CREATE PROCEDURE SP_SEL_NHANVIEN
AS
BEGIN
    OPEN SYMMETRIC KEY Prikey
    DECRYPTION BY CERTIFICATE Cert;
	SELECT NV.MANV, NV.HOTEN, NV.EMAIL,CONVERT(INT, DECRYPTBYKEY(NV.LUONG)) as LUONGCB
	FROM NHANVIEN AS NV
    CLOSE SYMMETRIC KEY Prikey
END
GO 
--exec SP_SEL_NHANVIEN
